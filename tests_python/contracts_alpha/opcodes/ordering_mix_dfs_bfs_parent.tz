# This contract intends to call "opcodes/ordering_concat_string_child.tz".
#
# Given a list, each elements of the list present the way to operation "opcodes/ordering_concat_string_child.tz"
#
# list (
#     pair
#        # the string is the input of ordering_concat_string_child.tz 
#        # and address is the address of ordering_concat_string_child.tz
#        (pair string address)
#
#        # The first bool is to control the order in BFS(True) or DFS(False) by ALLOW_DFS
#        # The second bool is to offer permission to run DFS by `ALLOW_DFS`
#        bool )
# )

{ parameter (list (pair (pair string address) bool)) ;
  storage unit ;
  code { NIL operation ;
         SWAP ;
         CAR ;
         ITER { DUP ;
                CDR ;
                SWAP ;
                CAR ;
                DUP ;
                CDR ;
                SWAP ;
                CAR ;
                SWAP ;
                CONTRACT string ;
                IF_NONE { PUSH string "Not a contract" ; FAILWITH } {} ;
                DIG 2 ;
                IF { DIG 2 ; SWAP ; PUSH mutez 0 ; DIG 3 ; TRANSFER_TOKENS ; CONS }
                   { DIG 2 ; SWAP ; PUSH mutez 0 ; DIG 3 ; TRANSFER_TOKENS ; MAKE_DFS ; CONS } } ;
         NIL operation ;
         SWAP ;
         PAIR ;
         LEFT (list operation) ;
         LOOP_LEFT
           { DUP ;
             CDR ;
             SWAP ;
             CAR ;
             IF_CONS
               { DIG 2 ; SWAP ; CONS ; SWAP ; PAIR ; LEFT (list operation) }
               { RIGHT (pair (list operation) (list operation)) } } ;
         PUSH unit Unit ;
         SWAP ;
         PAIR } }
